// This is a buildscript block
buildscript
{
    repositories {
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
        }
    }
}

plugins {
    // including this plugin directly instead of by an init script, which allows to use the freshly build version
    id 'org.jboss.pnc.gradle-manipulator.analyzer'
    id 'java-library'
    id 'maven-publish'
}

group = 'org.jboss.pnc.gradle-manipulator.analyzer.functest'
version = '1.0.0'

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        flatDir {
            dirs = [file("repo/")]
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
    }
}


dependencies {
    implementation "org.apache.commons:commons-lang3:latest.release"
    implementation('org.hdrhistogram:HdrHistogram:2.+')

    implementation group: 'io.undertow', name: 'undertow-core', version: '[2.0.0, 2.0.20)'
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation('com.typesafe:config:1.3.0')
    implementation('org.hibernate:hibernate-core:5.3.7.Final') // the version will be overridden by the following BOM to 5.3.9.Final
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.1.4.RELEASE')
    implementation('com.timgroup:java-statsd-client')
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'complex'

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
